/*
PlayerControl
@James/Argoms
    
    Player-specific control script
*/
class PlayerControl : ZilchComponent
{

    var MouseWasDown : Boolean = false; //used to check if the player is clicking or holding mouse down
    
    //internal for movement calculations:
    var LastMovement : Real3; //last movement vector of the player
    var FollowMouse : Boolean = false; //whether or not the player looks at the cursor
    
    //var Attacking : Boolean = false; //whether or not the player is currently attacking
    //DEPRECATED: attacking state now found in EntityAttack behavior
    
    var Weapon : EntityAttack; //contains info for doing damage
    
    var PlayerAnimation : SimpleAnimation; //player's animation
    var PlayerAnimationGraph : AnimationGraph; //player animation graph
    
    var ComboProgress : Integer = 1; //how many hits the player is into their combo
    var ComboCooldown : Real = 0; //time before combo resets
    
    //animations:
    [Property]
    var WalkAnimation : Animation;
    [Property]
    var IdleAnimation  : Animation;
    [Property]
    var BlockAnimation : Animation;
    
    
    [Property]
    var Attack1_1Animation : Animation;
    [Property]
    var Attack1_2Animation : Animation;
    [Property]
    var Attack1_3Animation : Animation;
    
    [Property]
    var Attack2_1Animation : Animation;
    [Property]
    var Attack2_2Animation : Animation;
    [Property]
    var Attack2_3Animation : Animation;
    
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        
        //set up player's camera
        var camera = this.Space.Create(Archetype.PlayerCamera);
        camera.PlayerCameraControl.MySprite = this.Owner.FindChildByName("Sprite");
        camera.PlayerCameraControl.MyPlayer = this.Owner;
        this.LevelSettings.CameraViewport.Camera = camera;
        
        //set up player's attacking:
        this.Weapon = this.Owner.EntityAttack;
        this.Weapon.Friendly = true;
        this.PlayerAnimation = this.Owner.FindChildByName("Mesh").FindChildByName("Mesh").SimpleAnimation;
        //this.PlayerAnimation.Animation = Animation.KnightAttack1_1;
        this.PlayerAnimationGraph = this.Owner.FindChildByName("Mesh").FindChildByName("Mesh").AnimationGraph;
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        
        //update mouse state:
        this.UpdateMouseInput();
        
        //update player translation:
        if(!this.Weapon.Attacking)
        {
            this.PlayerAnimationGraph.TimeScale = 1;
            if(Zero.Mouse.IsButtonDown(MouseButtons.Right))
            {
                this.PlayerAnimation.PlaySingle(this.BlockAnimation, AnimPlayMode.Loop);
                this.Owner.EntityHealth.Blocking = true;
                this.UpdateOrientation(event.Dt, false);
            } else {
                this.Owner.EntityHealth.Blocking = false;
                this.UpdateOrientation(event.Dt, true);
                this.ComboCooldown -= event.Dt;
                if(this.ComboCooldown <= 0)
                {
                    this.ComboProgress = 1;
                    this.ComboCooldown = 1;
                }
            }
        } else {
            
        }
    }
    
    /*
    UpdateOrientation
        handles the direction that the player is facing in
    */
    function UpdateOrientation(dt : Real, changeMovement : Boolean)
    {
        if(changeMovement)
        {
            if(Zero.Mouse.RawMovement != Real2(0,0))
            {
                this.FollowMouse = true;
                if(this.Owner.AutoOrientToVelocity != null)
                {
                    this.Owner.RemoveComponentByName("AutoOrientToVelocity");
                }
            }
            if(this.UpdateMovement(dt))
            {
                
                this.FollowMouse =false;
                //Console.WriteLine("potato");
                if(this.Owner.AutoOrientToVelocity == null)
                {
                    
                    this.Owner.AddComponentByName("AutoOrientToVelocity");
                }
            } 
            if(this.FollowMouse)
            {
                var cursor:Real2 = Zero.Mouse.ScreenPosition;
                var worldCursor:Real3 = this.LevelSettings.CameraViewport.ScreenToWorldZPlane(cursor, 0);
                var aim:Real3 = Math.Normalize(Real3(worldCursor.X-this.Owner.Transform.Translation.X, worldCursor.Y-this.Owner.Transform.Translation.Y, 0));
                this.Owner.Orientation.LookAtDirectionWithUp(aim, Real3(0,0,1));
                //Console.WriteLine(this.Owner.Orientation.WorldForward);
            }
        } else {
            var cursor:Real2 = Zero.Mouse.ScreenPosition;
            var worldCursor:Real3 = this.LevelSettings.CameraViewport.ScreenToWorldZPlane(cursor, 0);
            var aim:Real3 = Math.Normalize(Real3(worldCursor.X-this.Owner.Transform.Translation.X, worldCursor.Y-this.Owner.Transform.Translation.Y, 0));
            this.Owner.Orientation.LookAtDirectionWithUp(aim, Real3(0,0,1));
        }
    }
    
    /*
    UpdateMouseInput
        handles mouse press/release function calls
    */
    function UpdateMouseInput()
    {
        
        if(!this.MouseWasDown)
        {
            if(Zero.Mouse.IsButtonDown(MouseButtons.Left))
            {
                this.OnMousePressed();
                this.MouseWasDown = true;
            }
        } else {
            if(!Zero.Mouse.IsButtonDown(MouseButtons.Left))
            {
                this.OnMouseReleased();
                this.MouseWasDown = false;
            }
        }
    }
    
    /*
    UpdateMovement
        calculates movement vector based on key input, applies to EntityMovement
        returns whether or not this is the moment where the object just started moving
    */
    function UpdateMovement(dt : Real) : Boolean
    {
        var movement:Real3 = Real3(0, 0, 0); //gets movement direction
        if(Zero.Keyboard.KeyIsDown(Keys.A))
        {
            movement += Real3 (-1, 0, 0);
        }
        if(Zero.Keyboard.KeyIsDown(Keys.D))
        {
            movement += Real3 (1, 0, 0);
        }
        if(Zero.Keyboard.KeyIsDown(Keys.W))
        {
            movement += Real3 (0, 1, 0);
        }
        if(Zero.Keyboard.KeyIsDown(Keys.S))
        {
            movement += Real3 (0, -1, 0);
        }
        Math.Normalize(movement);
        this.Owner.EntityMovement.Move(movement, dt);
        
        var wasJustStill : Boolean = this.LastMovement == Real3(0,0,0);
        this.LastMovement = movement;
        
        //change to walking animation if appropriate:
        if(this.PlayerAnimation.Animation != this.WalkAnimation && movement != Real3(0,0,0))
        {
            //this.PlayerAnimation.PlaySingle(Animation.KnightWalk, AnimPlayMode.Loop);
            this.PlayerAnimation.Animation = this.WalkAnimation;
            this.PlayerAnimation.PlayMode = AnimPlayMode.Loop;
        } else {
            //idle stance if not moving
            if(movement == Real3(0,0,0))
            {
                this.PlayerAnimation.Animation = this.IdleAnimation;
            }
        }
        //this.PlayerAnimation.Animation = Animation.KnightIdle;
        
        return movement != Real3(0,0,0) && wasJustStill;// && this.MovementStopTest != Real3(0,0,0);
        
    }
    
    /*
    Attack
        Tells the player's weapon component to attack
    */
    function Attack()
    {
        
        var cursor:Real2 = Zero.Mouse.ScreenPosition;
        var worldCursor:Real3 = this.LevelSettings.CameraViewport.ScreenToWorldZPlane(cursor, 0);
        var aim:Real3 = Math.Normalize(Real3(worldCursor.X-this.Owner.Transform.Translation.X, worldCursor.Y-this.Owner.Transform.Translation.Y, 0));
        
        this.PlayerAnimation.Animation = null;
        if(this.ComboProgress == 1)
        {
            //forward step:
            this.Owner.RigidBody.ApplyLinearVelocity(aim*5);
            this.Weapon.Damage = 1;
            
            //configure weapon:
            this.Weapon.BaseCooldown = 0.1;
            this.Weapon.BaseWindup = 0.1;
            this.Weapon.AttackCollision = MultiConvexMesh.KnightAttackTracer1_1;
            this.Weapon.Knockback = 5;
            
            //animation play:
            this.PlayerAnimation.PlaySingle(this.Attack1_1Animation, AnimPlayMode.PlayOnce);
            this.PlayerAnimationGraph.TimeScale = 2;
        } else if(this.ComboProgress == 2)
        {
            //forward step:
            this.Owner.RigidBody.ApplyLinearVelocity(aim*5);
            
            //configure weapon:
            this.Weapon.BaseCooldown = 0.1;
            this.Weapon.BaseWindup = 0.1;
            this.Weapon.AttackCollision = MultiConvexMesh.KnightAttackTracer1_2;
            this.Weapon.Damage = 1;
            this.Weapon.Knockback = 5;
            
            //animation play:
            this.PlayerAnimation.PlaySingle(this.Attack1_2Animation, AnimPlayMode.PlayOnce);
            this.PlayerAnimationGraph.TimeScale = 2;
        } else if(this.ComboProgress == 3)
        {
            //forward step:
            this.Owner.RigidBody.ApplyLinearVelocity(aim*15);
            
            //configure weapon:
            this.Weapon.BaseCooldown = 0.4;
            this.Weapon.BaseWindup = 0.1;
            this.Weapon.AttackCollision = MultiConvexMesh.KnightAttackTracer1_3;
            this.Weapon.Damage = 2;
            this.Weapon.Knockback = 25;
            
            //animation play:
            this.PlayerAnimation.PlaySingle(this.Attack1_3Animation, AnimPlayMode.PlayOnce);
            this.PlayerAnimationGraph.TimeScale = 1.5;
        }
        
        this.Weapon.Attack(aim);
        
        this.ComboProgress += 1;
        if(this.ComboProgress > 3)
        {
            this.ComboProgress = 1;
        }
        //this.PlayerAnimation.PlayMode = AnimPlayMode.PlayOnce;
    }
    
    function OnMousePressed()
    {
        if(!this.Weapon.Attacking)
        {
            this.Attack();
        }
    }
    
    function OnMouseReleased()
    {
    }
}
